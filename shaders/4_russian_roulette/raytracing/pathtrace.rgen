#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_nonuniform_qualifier : enable

#include "../lib/uniform_bindings.glsl"
#include "../rt/bindings.glsl"

#include "../lib/payload.glsl"
#include "../lib/rand.glsl"
#include "../lib/brdf2.glsl"
#include "../lib/conversions.glsl"
#include "../lib/material.glsl"
#include "../lib/tonemap.glsl"
#include "../lib/colors.glsl"
#include "../lib/utils.glsl"

layout(set = 1, binding = 0, rgba32f) uniform image2D accumulation;
layout(set = 1, binding = 1, rgba8) uniform image2D outImage;

layout(location = 0) rayPayloadEXT Payload payload;

void main() {
	ivec2 pixel = ivec2(gl_LaunchIDEXT.xy);
    bool right = pixel.x > 513;
    bool bottom = pixel.y > 316;
    ivec2 originalPixel = pixel;
    pixel.x %= 513;
    pixel.y %= 316;
    uvec2 resolution = uvec2(513,316);
    uint state = initRNG(uvec2(pixel), resolution, uni.frame);

	vec2 uv = vec2(pixel) / vec2(resolution);
    uv.y = 1.0 - uv.y;
    vec3 viewPos = screenToView(uni.projInverse, uv, 1.0);

    vec3 origin = uni.viewInverse[3].xyz;
    vec3 direction = mat3(uni.viewInverse) * normalize(viewPos);

    vec3 throughput = vec3(1.0);
    vec3 radiance = vec3(0.0);
    bool inside = false;

    float smoothnessFactor = 1.0;
    for (int i = 0; i < 5; i++) {
        payload.dist = -1.0;
        traceRayEXT(
            tlas,
            gl_RayFlagsNoneEXT, 
            0xFF,               // mask
            0,					// sbtRecordOffset
            0,					// sbtRecordStride
            0,					// missIndex
            origin,
            0.001,
            direction,
            256.0,
            0					// payloadLocation
        );

        if (payload.dist < 0.0) {
            // Sky
            radiance += throughput * SKY_COLOR;
            break;
        }

        Material material = payload.material;
        vec3 normal = payload.normal;
    
        radiance += throughput * material.emission;

        vec3 nextDir = sampleCosineWeighted(state, normal);
        float pdf = dot(normal, nextDir) / PI;
        vec3 brdf = material.baseColor.rgb;//brdfDirect(material, payload.normal, -direction, nextDir);

        vec3 hitPosition = origin + direction * payload.dist;
        throughput *= brdf;
        origin = hitPosition + normal * 0.01;
        direction = nextDir;

        if (right) {
            float chance = dot(throughput, vec3(0.299, 0.587, 0.114));
            if (randFloat(state) < chance) {
                break;
            }
            throughput /= 1.0 - chance;
        }
    }
    vec4 previous = imageLoad(accumulation, originalPixel);
    vec4 value;

    if (isNanVec(radiance)) {
        value = previous;
    } else if (!moved(uni.view, prev.view)) {
        if (previous.a > (bottom ? 100000 : 10)) {
            value = previous;
        } else {
            value = vec4(
                mix(previous.rgb, radiance, 1.0 / (previous.a + 1.0)),
                previous.a + 1.0
            );
        }
    } else {
        value = vec4(radiance, 1.0);
    }
    imageStore(accumulation, originalPixel, value);
    imageStore(outImage, originalPixel, vec4(tonemap(value.rgb), 1.0));
}